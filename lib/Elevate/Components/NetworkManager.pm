package Elevate::Components::NetworkManager;

=encoding utf-8

=head1 NAME

Elevate::Components::NetworkManager

=head2 check

Verify that NetworkManager's dhcp setting is not configured to dhclient.  Leapp
will inhibit the upgrade for this and we do not want to touch this network
setting.

=head2 pre_distro_upgrade

Enable the NetworkManager service so that it will start on the next reboot

Convert network-scripts configuration files to NetworkManager on servers where
network-scripts style configuration files are no longer supported

=head2 post_distro_upgrade

noop

=cut

use cPstrict;

use Elevate::Constants        ();
use Elevate::OS               ();
use Elevate::SystemctlService ();

use Log::Log4perl qw(:easy);

use parent qw{Elevate::Components::Base};

use constant DHCP_CLIENT_CONF => '/usr/lib/NetworkManager/conf.d/10-dhcp-dhclient.conf';
use constant NETWORKMANAGER   => '/usr/sbin/NetworkManager';
use constant NMCLI_PATH       => '/usr/bin/nmcli';

sub check ($self) {
    return unless Elevate::OS::needs_leapp();
    return if Elevate::OS::network_scripts_are_supported();

    $self->_autofix_dhcp_dhclient_added_by_leapp();

    my $networkmanager = NETWORKMANAGER();
    my $out            = $self->ssystem_hide_and_capture_output( $networkmanager, '--print-config' );

    if ( $out->{status} != 0 ) {
        my $status = $out->{status};
        return $self->has_blocker(<<~"EOS");
        An error code ($status) was returned when attempting to determine the NetworkManager on this system.
        To attempt to retrieve the configuration, you can execute:

        $networkmanager --print-config

        Resolve the issue with this command before proceeding.
        EOS
    }

    foreach my $line ( @{ $out->{stdout} } ) {
        next unless $line =~ m/^\s*dhcp\s*=\s*(.*)/;
        my $dhcp_setting = $1;

        if ( $dhcp_setting eq 'dhclient' ) {
            my $pretty_distro_name = Elevate::OS::upgrade_to_pretty_name();
            return $self->has_blocker(<<~"EOS");
            NetworkManager is configured to usage with the "dhclient" DHCP module. This
            setting will be ignored after upgrading to $pretty_distro_name.  To see your
            config, execute the following command:

            $networkmanager --print-config

            Review the current configuration for this server and remove or update it to set
            dhcp to something other than dhclient.
            EOS
        }
    }

    return;
}

sub pre_distro_upgrade ($self) {
    return unless Elevate::OS::needs_leapp();
    return if $self->upgrade_distro_manually();

    $self->_enable_network_manager();
    $self->run_once('_convert_network_scripts_to_network_manager');

    return;
}

sub _enable_network_manager ($self) {
    return unless Elevate::OS::needs_network_manager();

    my $service_name = 'NetworkManager';
    my $service      = Elevate::SystemctlService->new( name => $service_name );

    return if $service->is_enabled();
    $service->enable();
    return;
}

sub _convert_network_scripts_to_network_manager ($self) {
    return if Elevate::OS::network_scripts_are_supported();

    my $glob_path   = Elevate::Constants::ETH_FILE_PREFIX() . '*';
    my @ifcfg_files = grep { $_ !~ m{/ifcfg-lo$} } glob $glob_path;
    foreach my $file (@ifcfg_files) {

        # Convert these to use NetworkManager using the suggested command from the inhibitor
        # Legacy network configuration found
        # Network configuration files in legacy "ifcfg" format are present.In Red Hat Enterprise Linux 10, support for these files is no longer enabled and the configuration will be ignored. The following files were found:
        # - /etc/sysconfig/network-scripts/ifcfg-eth0
        # Possible resolution: Convert the configuration into NetworkManager native "keyfile" format.
        # Consider running:
        # nmcli connection migrate /etc/sysconfig/network-scripts/ifcfg-eth0
        $self->ssystem_and_die( NMCLI_PATH(), 'connection', 'migrate', $file );
    }

    return;
}

=head1

This file is generated when upgrading from C7->A8.  It is not needed / has no
affect since cPanel servers will still use network-scripts on A8.  Since the
file creates a blocker when upgrading from A9->A10 due to dhclient
configurations being ignored on A10, lets go ahead and remove this file before
performing the check.

=cut

sub _autofix_dhcp_dhclient_added_by_leapp ($self) {
    return unless -s DHCP_CLIENT_CONF();

    my $txt = File::Slurper::read_binary(DHCP_CLIENT_CONF);
    return unless $txt =~ m/Generated by leapp when upgrading/g;

    unlink DHCP_CLIENT_CONF();

    return;
}

1;
