#!/usr/local/cpanel/3rdparty/bin/perl

#                                      Copyright 2024 WebPros International, LLC
#                                                           All rights reserved.
# copyright@cpanel.net                                         http://cpanel.net
# This code is subject to the cPanel license. Unauthorized copying is prohibited.

package test::cpev::components;

use FindBin;

use Test2::V0;
use Test2::Tools::Explain;
use Test2::Plugin::NoWarnings;
use Test2::Tools::Exception;

use Test::MockFile 0.032;
use Test::MockModule qw/strict/;

use lib $FindBin::Bin . "/lib";
use Test::Elevate;

use cPstrict;

my $nm = cpev->new->get_component('NetworkManager');

{
    note 'Testing check';

    my $mock_nm = Test::MockModule->new('Elevate::Components::NetworkManager');
    $mock_nm->redefine(
        _autofix_dhcp_dhclient_added_by_leapp => sub { die "Do not call this yet\n"; },
    );

    my %os_hash = (
        alma   => [8],
        cent   => [7],
        ubuntu => [ 20, 22 ],
    );
    foreach my $distro ( sort keys %os_hash ) {
        foreach my $version ( @{ $os_hash{$distro} } ) {
            set_os_to( $distro, $version );
            try_ok { $nm->check() } 'Returns early when network_scripts_are_supported or leapp is not needed';
        }
    }

    set_os_to_almalinux_9();

    $mock_nm->redefine(
        _autofix_dhcp_dhclient_added_by_leapp => 1,
    );

    my $status;
    my @cmds;
    my @stdout;
    my $mock_cpev = Test::MockModule->new('cpev');
    $mock_cpev->redefine(
        ssystem_hide_and_capture_output => sub ( $, @args ) {
            push @cmds, [@args];
            return { status => $status, stdout => \@stdout, stderr => [] };
        },
    );

    $status = 1;

    like(
        $nm->check(),
        {
            id  => 'Elevate::Components::NetworkManager::check',
            msg => qr/An error code \(1\) was returned when/
        },
        'Blocker received when the NetworkManager command fails to return cleanly',
    );

    is(
        \@cmds,
        [
            [
                '/usr/sbin/NetworkManager',
                '--print-config',
            ],
        ],
        'Correct system commands were executed',
    );

    undef @cmds;
    $status = 0;
    @stdout = get_mock_networkmanager_config_data();
    push @stdout, 'dhcp=dhclient';

    like(
        $nm->check(),
        {
            id  => 'Elevate::Components::NetworkManager::check',
            msg => qr/NetworkManager is configured to usage with the "dhclient" DHCP module/,
        },
        'Blocker received when DHCP is set configured to use the dhclient module',
    );

    is(
        \@cmds,
        [
            [
                '/usr/sbin/NetworkManager',
                '--print-config',
            ],
        ],
        'Correct system commands were executed',
    );

    undef @cmds;
    @stdout = get_mock_networkmanager_config_data();

    is( $nm->check(), undef, 'No blocker given when dhcp is not configured to dhclient' );
    is(
        \@cmds,
        [
            [
                '/usr/sbin/NetworkManager',
                '--print-config',
            ],
        ],
        'Correct system commands were executed',
    );
}

{
    note 'Testing _autofix_dhcp_dhclient_added_by_leapp';

    my $conf_file = '/usr/lib/NetworkManager/conf.d/10-dhcp-dhclient.conf';

    my $mock_conf = Test::MockFile->file( $conf_file, 'mocked' );

    my $mock_file_slurper = Test::MockModule->new('File::Slurper');
    $mock_file_slurper->redefine(
        read_binary => sub { return "custom user file\n"; },
    );

    try_ok { $nm->_autofix_dhcp_dhclient_added_by_leapp() } "Lives when called";
    ok( -e $conf_file, "$conf_file should NOT be removed when it was not generated by leapp" );

    $mock_file_slurper->redefine(
        read_binary => sub { return "Generated by leapp when upgrading\ndhcp=dhclient\n"; },
    );

    try_ok { $nm->_autofix_dhcp_dhclient_added_by_leapp() } "Lives when called";
    ok( !-e $conf_file, "$conf_file should be removed when it is generated by leapp" );

    $mock_file_slurper->redefine(
        read_binary => sub { die "Do not call this\n"; },
    );

    unlink $conf_file;

    is( $nm->_autofix_dhcp_dhclient_added_by_leapp(), undef, "Returns early when $conf_file does not exist" );
}

{
    note 'Testing pre_distro_upgrade';

    my $mock_nm = Test::MockModule->new('Elevate::Components::NetworkManager');
    $mock_nm->redefine(
        _enable_network_manager => sub { die "Do not call this\n"; },
    );

    foreach my $version ( '20', '22' ) {
        set_os_to( 'ubuntu', $version );
        try_ok { $nm->pre_distro_upgrade() } 'Returns early when leapp is not in use';
    }

    set_os_to_almalinux_8();

    my $mock_cpev = Test::MockModule->new('cpev');
    $mock_cpev->redefine( upgrade_distro_manually => 1 );

    try_ok { $nm->pre_distro_upgrade() } 'Returns early when upgrading the distro manually';

    $mock_cpev->redefine( upgrade_distro_manually => 0 );

    my $called = 0;
    $mock_nm->redefine(
        _enable_network_manager => sub { $called++ },
        run_once                => sub { $called++ },
    );

    try_ok { $nm->pre_distro_upgrade() } 'Completes okay when it does not return early';
    is( $called, 2, 'Expected subs were called' );
}

{
    note "checking _enable_network_manager";

    my $mock_systemctl = Test::MockModule->new('Elevate::SystemctlService');
    $mock_systemctl->redefine(
        is_enabled => sub { die "do not call this yet\n"; },
    );

    my %os_hash = (
        cent   => [7],
        ubuntu => [ 20, 22 ],
    );
    foreach my $distro ( keys %os_hash ) {
        foreach my $version ( @{ $os_hash{$distro} } ) {
            set_os_to( $distro, $version );

            is( $nm->_enable_network_manager(), undef, 'Returns early when network manager does not need to be enabled prior to the distro upgrade' );
        }
    }

    %os_hash = (
        alma => [ 8, 9 ],
    );
    foreach my $distro ( keys %os_hash ) {
        foreach my $version ( @{ $os_hash{$distro} } ) {
            set_os_to( $distro, $version );

            my $called_enabled = 0;
            $mock_systemctl->redefine(
                is_enabled => 1,
                enable     => sub { $called_enabled++; },
            );

            is( $nm->_enable_network_manager(), undef, 'Return undef when the network manager service is already enabled' );
            is( $called_enabled,                0,     'Does not attempt to enable network manager if the service is already enabled' );

            $mock_systemctl->redefine(
                is_enabled => 0,
            );

            is( $nm->_enable_network_manager(), undef, 'Return undef when it enables the network manager service' );
            is( $called_enabled,                1,     'Enables the network manager service if it is not already enabled' );
        }
    }
}

{
    note 'Testing _convert_network_scripts_to_network_manager';

    my $mock_dir = Test::MockFile->dir('/etc/sysconfig/network-scripts');
    mkdir '/etc/sysconfig/network-scripts';

    my $mock_eth0 = Test::MockFile->file( '/etc/sysconfig/network-scripts/ifcfg-eth0', 'mocked' );
    my $mock_eth1 = Test::MockFile->file( '/etc/sysconfig/network-scripts/ifcfg-eth1', 'mocked' );

    my $mock_cpev = Test::MockModule->new('cpev');
    $mock_cpev->redefine(
        ssystem_and_die => sub { die "Do not call this yet\n"; },
    );

    my %os_hash = (
        cent  => 7,
        alma  => 8,
        cloud => 8,
    );
    foreach my $distro ( sort keys %os_hash ) {
        set_os_to( $distro, $os_hash{$distro} );
        try_ok { $nm->_convert_network_scripts_to_network_manager() } 'Returns early when network scripts are supported';
    }

    set_os_to_almalinux_9();

    $mock_cpev->redefine(
        ssystem_and_die => sub { die "I'm sorry Dave, I'm afraid I can't do that\n"; },
    );

    my $err = dies { $nm->_convert_network_scripts_to_network_manager() };
    is( $err, "I'm sorry Dave, I'm afraid I can't do that\n", 'Dies when the system command to convert the configuration to NetworkManager fails' );

    my @cmds;
    $mock_cpev->redefine(
        ssystem_and_die => sub ( $, @args ) {
            push @cmds, [@args];
            return;
        },
    );

    try_ok { $nm->_convert_network_scripts_to_network_manager() } 'Lives when it successfully converts the configs to NetworkManager';

    is(
        \@cmds,
        [
            [
                '/usr/bin/nmcli',
                'connection',
                'migrate',
                '/etc/sysconfig/network-scripts/ifcfg-eth0',
            ],
            [
                '/usr/bin/nmcli',
                'connection',
                'migrate',
                '/etc/sysconfig/network-scripts/ifcfg-eth1',
            ],
        ],
        'The expected commands are executed'
    );

    unlink '/etc/sysconfig/network-scripts/ifcfg-eth0';
    unlink '/etc/sysconfig/network-scripts/ifcfg-eth1';

    undef @cmds;

    try_ok { $nm->_convert_network_scripts_to_network_manager() } 'Lives when it successfully converts the configs to NetworkManager';

    is( \@cmds, [], 'No commands executed when no network-scripts configurations are found' );
}

sub get_mock_networkmanager_config_data {
    my $data = <<'EOS';
# NetworkManager configuration: /etc/NetworkManager/NetworkManager.conf, /etc/NetworkManager/conf.d/30-cloud-init-ip6-addr-gen-mode.conf

[main]
# plugins=
# rc-manager=auto
# migrate-ifcfg-rh=false
# auth-polkit=true
# dhcp=internal
# iwd-config-path=
configure-and-quit=no

[logging]
# backend=journal
# audit=false

[device]
# wifi.backend=wpa_supplicant

[connection.30-cloud-init-ip6-addr-gen-mode]
ipv6.addr-gen-mode=0

# no-auto-default file "/var/lib/NetworkManager/no-auto-default.state"
EOS

    my @data_array = split "\n", $data;
    return @data_array;
}

done_testing();
